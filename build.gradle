plugins {
  id 'java'
}

version '1.0-SNAPSHOT'

allprojects {

  group 'com.github.apycazo.snippets'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenLocal()
    mavenCentral()
  }

  clean.doLast {
    delete "${projectDir}/out" // TODO: fix this, does not remove directories in modules
  }

  ext {
    logbackStack = [
      "org.slf4j:slf4j-api:${slf4jVersion}",
      "ch.qos.logback:logback-core:${logbackVersion}",
      "ch.qos.logback:logback-classic:${logbackVersion}"
    ]

    log4j2Stack = [
      "org.slf4j:slf4j-api:${slf4jVersion}",
      "org.apache.logging.log4j:log4j-core:${log4j2Version}",
      "org.apache.logging.log4j:log4j-api:${log4j2Version}",
      "org.apache.logging.log4j:log4j-slf4j-impl:${log4j2Version}"
    ]

    serializationStack = [
      "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
      "org.yaml:snakeyaml:${snakeYamlVersion}"
    ]

    springContext = [
      "org.springframework:spring-context:${springVersion}"
    ]

    jerseyStack = [
      "org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}",
      "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}",
      "org.glassfish.jersey.ext:jersey-spring4:${jerseyVersion}",
      "org.glassfish.jersey.ext:jersey-bean-validation:${jerseyVersion}",
      "javax.servlet:javax.servlet-api:${servletApiVersion}",
      "javax.xml.bind:jaxb-api:${jaxbApiVersion}"
    ]

    jettyStack = [
      "org.eclipse.jetty:jetty-server:${jettyVersion}",
      "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    ]

    testStack = [
      "org.springframework:spring-test:${springVersion}",
      "junit:junit:${junitVersion}",
      "org.assertj:assertj-core:${assertjVersion}",
      "io.cucumber:cucumber-java:${cucumberVersion}",
      "io.cucumber:cucumber-junit:${cucumberVersion}",
      "io.cucumber:cucumber-spring:${cucumberVersion}",
      "io.rest-assured:rest-assured:${restAssuredVersion}"
    ]

  }
}

subprojects {

  apply plugin: 'maven-publish'

  publishing {
    publications {
      main(MavenPublication) {
        from components.java
      }
    }
  }

  // TODO review this
  jar {
    manifest {
      attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version
      )
    }
  }
}



